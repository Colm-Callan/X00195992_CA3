plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.calculator'
version = '0.0.2-SNAPSHOT'

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
}

repositories { mavenCentral() }

dependencies {
    implementation 'com.sparkjava:spark-core:2.9.4'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
}

application {
    mainClass = 'org.calculator.WebServer'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}
jacocoTestReport.finalizedBy jacocoTestCoverageVerification

sonarqube {
    properties {
        property "sonar.projectKey", "Colm-Callan_DevOpsJavaCalc"
        property "sonar.organization", "colmcallan"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.java.binaries", "${buildDir}/classes/java/main"
    }
}

tasks.named('shadowJar') {
    archiveBaseName.set('calculator')
    archiveClassifier.set('all')
}